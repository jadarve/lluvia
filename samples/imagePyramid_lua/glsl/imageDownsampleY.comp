#version 450

#include <lluvia/core.glsl>

layout(binding = 0, rgba8ui) uniform uimage2D in_RGBA;
layout(binding = 1, rgba8ui) uniform uimage2D out_RGBA;

void main() {

    const ivec2 coords  = LL_GLOBAL_COORDS_2D;
    const ivec2 imgSize = imageSize(out_RGBA);

    if (coords.x > imgSize.x || coords.y > imgSize.y) {
        return;
    }

    const bool topBorder    = coords.y == 0;
    const bool bottomBorder = coords.y == imgSize.y -1;

    const ivec2 inputImgCoords = ivec2(coords.x, 2*coords.y);

    const uvec4 img_0 = imageLoad(in_RGBA, inputImgCoords);

    const uvec4 img_m = topBorder    ? img_0 : imageLoad(in_RGBA, inputImgCoords + ivec2(0, -1));
    const uvec4 img_p = bottomBorder ? img_0 : imageLoad(in_RGBA, inputImgCoords + ivec2(0, +1));    

    // this is equivalent to 0.5*img_0 + 0.25*(img_m + img_p)
    const uvec4 filterValue = (img_0 >> 1) + ((img_m + img_p) >> 2);

    imageStore(out_RGBA, coords, filterValue);
}
